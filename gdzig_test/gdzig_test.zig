pub fn runInit(comptime testcase: type, current: InitializationLevel) void {
    const level: InitializationLevel = if (@hasDecl(testcase, "level"))
        testcase.level
    else
        .scene;

    if (current != level) return;

    if (@hasDecl(testcase, "init")) {
        callTestFn(testcase.init) catch |e| fail(e);
    }
    if (@hasDecl(testcase, "run")) {
        callTestFn(testcase.run) catch |e| fail(e);
    }
}

pub fn runDeinit(comptime testcase: type, current: InitializationLevel) void {
    const level: InitializationLevel = if (@hasDecl(testcase, "level"))
        testcase.level
    else
        .scene;

    if (current != level) return;

    if (@hasDecl(testcase, "deinit")) {
        callTestFn(testcase.deinit) catch |e| fail(e);
    }

    switch (allocator_instance.deinit()) {
        .ok => pass(),
        .leak => fail(error.Leak),
    }
}

fn callTestFn(comptime func: anytype) !void {
    const ReturnType = @typeInfo(@TypeOf(func)).@"fn".return_type.?;
    if (ReturnType == void) {
        func();
    } else if (@typeInfo(ReturnType) == .error_union) {
        try func();
    } else {
        @compileError("test function must return void or !void");
    }
}

fn pass() noreturn {
    std.process.exit(0);
}

fn fail(err: anyerror) noreturn {
    std.debug.print("{s}\n", .{@errorName(err)});
    std.process.exit(1);
}

pub fn expectCall(object: anytype, comptime name: [:0]const u8, args: anytype, expected: anytype) !void {
    const method: StringName = .fromComptimeLatin1(name);

    var result = Object.callAlloc(.upcast(object), method, args);
    defer result.deinit();

    const val = result.as(@TypeOf(expected)) orelse return error.InvalidResult;
    try expectEqual(expected, val);
}

const std = @import("std");
pub const FailingAllocator = std.testing.FailingAllocator;
pub const FuzzInputOptions = std.testing.FuzzInputOptions;
pub const Reader = std.testing.Reader;
pub const TmpDir = std.testing.TmpDir;
pub const allocator = allocator_instance.allocator();
pub var allocator_instance: std.heap.GeneralPurposeAllocator(.{
    .stack_trace_frames = if (std.debug.sys_can_stack_trace) 10 else 0,
    .resize_stack_traces = true,
}) = .{
    .backing_allocator = godot.engine_allocator,
};
pub const backend_can_print = std.testing.backend_can_print;
pub const checkAllAllocationFailures = std.testing.checkAllAllocationFailures;
pub const expect = std.testing.expect;
pub const expectApproxEqAbs = std.testing.expectApproxEqAbs;
pub const expectApproxEqRel = std.testing.expectApproxEqRel;
pub const expectEqual = std.testing.expectEqual;
pub const expectEqualDeep = std.testing.expectEqualDeep;
pub const expectEqualSentinel = std.testing.expectEqualSentinel;
pub const expectEqualSlices = std.testing.expectEqualSlices;
pub const expectEqualStrings = std.testing.expectEqualStrings;
pub const expectError = std.testing.expectError;
pub const expectFmt = std.testing.expectFmt;
pub const expectStringEndsWith = std.testing.expectStringEndsWith;
pub const expectStringStartsWith = std.testing.expectStringStartsWith;
pub const failing_allocator = std.testing.failing_allocator;
pub const fuzz = std.testing.fuzz;
pub const refAllDecls = std.testing.refAllDecls;
pub const refAllDeclsRecursive = std.testing.refAllDeclsRecursive;
pub const tmpDir = std.testing.tmpDir;

const godot = @import("gdzig");
const InitializationLevel = godot.global.InitializationLevel;
const Object = godot.class.Object;
const StringName = godot.builtin.StringName;
